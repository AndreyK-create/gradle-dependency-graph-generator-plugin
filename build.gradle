buildscript {
  ext.kotlinVersion = '1.3.50'

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    mavenLocal()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    classpath 'com.gradle.publish:plugin-publish-plugin:0.10.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.25.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.19.0'
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'
  }
}

apply plugin: 'groovy'
apply plugin: 'java-library'
apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply plugin: "com.vanniktech.maven.publish"
apply plugin: 'com.gradle.plugin-publish'

codeQualityTools {
  ktlint {
    toolVersion = '0.34.2'
  }
  detekt {
    toolVersion = '1.0.1'
  }
  pmd {
    enabled = false
  }
  checkstyle {
    enabled = false
  }
  cpd {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

gradlePlugin {
  plugins {
    graphGeneratorDependencyPlugin {
      id = 'com.vanniktech.dependency.graph.generator'
      implementationClass = 'com.vanniktech.dependency.graph.generator.DependencyGraphGeneratorPlugin'
    }
  }
}

repositories {
  mavenCentral()
  google()
  jcenter()
}

dependencies {
  implementation localGroovy()
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
  implementation gradleApi()
  api "guru.nidi:graphviz-java:0.9.0"

  testImplementation 'junit:junit:4.13-beta-3'
  testImplementation 'org.assertj:assertj-core:2.9.1'
  testImplementation 'com.android.tools.build:gradle:3.5.1'
  testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
}

sourceCompatibility = JavaVersion.VERSION_1_7

pluginBundle {
  website = POM_URL
  vcsUrl = POM_SCM_URL

  plugins {
    graphGeneratorDependencyPlugin {
      displayName = POM_NAME
      tags = ['gradle', 'graph', 'dependencies', 'graphviz', 'dot']
      description = POM_DESCRIPTION
    }
  }
}

wrapper {
  gradleVersion = '5.6.2'
  distributionType = Wrapper.DistributionType.ALL
}
